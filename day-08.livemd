<!-- livebook:{"persist_outputs":true} -->

# Day 8: Treetop Tree House

```elixir
tree_map = Path.join(__DIR__, "day-08.data") |> File.read!()

ExUnit.start(autorun: false)
```

<!-- livebook:{"output":true} -->

```
:ok
```

## Part One

The expedition comes across a peculiar patch of tall trees all planted carefully in a grid. The Elves explain that a previous expedition planted these trees as a reforestation effort. Now, they're curious if this would be a good location for a [tree house](https://en.wikipedia.org/wiki/Tree_house).

First, determine whether there is enough tree cover here to keep a tree house **hidden**. To do this, you need to count the number of trees that are **visible from outside the grid** when looking directly along a row or column.

The Elves have already launched a [quadcopter](https://en.wikipedia.org/wiki/Quadcopter) to generate a map with the height of each tree (your puzzle input). For example:

```elixir
sample_tree_map = """
30373
25512
65332
33549
35390
"""
```

<!-- livebook:{"output":true} -->

```
"30373\n25512\n65332\n33549\n35390\n"
```

Each tree is represented as a single digit whose value is its height, where `0` is the shortest and `9` is the tallest.

A tree is **visible** if all of the other trees between it and an edge of the grid are **shorter** than it. Only consider trees in the same row or column; that is, only look up, down, left, or right from any given tree.

```elixir
defmodule TreeMap do
  def parse(input) do
    input
    |> String.trim()
    |> String.split("\n")
    |> Enum.map(fn line ->
      line
      |> String.codepoints()
      |> Enum.map(&String.to_integer/1)
    end)
  end

  def visible?(map, row, col) do
    tree_height = tree(map, row, col)

    Enum.any?(corridors(map, row, col), fn corridor ->
      Enum.all?(corridor, &(&1 < tree_height))
    end)
  end

  def scenic_score(map, row, col) do
    treehouse = tree(map, row, col)

    Enum.map(corridors(map, row, col), fn corridor ->
      Enum.reduce_while(corridor, [], fn tree, visible_trees ->
        if tree >= treehouse do
          {:halt, [tree | visible_trees]}
        else
          {:cont, [tree | visible_trees]}
        end
      end)
      |> length()
    end)
    |> Enum.reduce(&*/2)
  end

  defp tree(map, row, col), do: map |> Enum.at(row) |> Enum.at(col)

  defp map_row(map, row), do: Enum.at(map, row)
  defp map_col(map, col), do: Enum.map(map, &Enum.at(&1, col))

  defp corridors(map, row, col) do
    {left, right} = map |> map_row(row) |> Enum.split(col)
    {top, bottom} = map |> map_col(col) |> Enum.split(row)

    [Enum.reverse(top), tl(right), tl(bottom), Enum.reverse(left)]
  end
end
```

<!-- livebook:{"output":true} -->

```
{:module, TreeMap, <<70, 79, 82, 49, 0, 0, 16, ...>>, {:corridors, 3}}
```

All of the trees around the edge of the grid are **visible** - since they are already on the edge, there are no trees to block the view. In this example, that only leaves the **interior nine trees** to consider:

* The top-left `5` is **visible** from the left and top. (It isn't visible from the right or bottom since other trees of height `5` are in the way.)
* The top-middle `5` is **visible** from the top and right.
* The top-right `1` is not visible from any direction; for it to be visible, there would need to only be trees of height `0` between it and an edge.
* The left-middle `5` is **visible**, but only from the right.
* The center `3` is not visible from any direction; for it to be visible, there would need to be only trees of at most height `2` between it and an edge.
* The right-middle `3` is **visible** from the right.
* In the bottom row, the middle `5` is **visible**, but the `3` and `4` are not.

```elixir
defmodule TreeVisibilityTest do
  use ExUnit.Case

  @tree_visibility [
    [true, true, true, true, true],
    [true, true, true, false, true],
    [true, true, false, true, true],
    [true, false, true, false, true],
    [true, true, true, true, true]
  ]

  setup do: [map: TreeMap.parse(unquote(sample_tree_map))]

  for {trees, row} <- Enum.with_index(@tree_visibility) do
    for {tree_visible?, col} <- Enum.with_index(trees) do
      test "tree at (#{row}, #{col}) is #{if !tree_visible?, do: "not "}visible", %{map: map} do
        if unquote(tree_visible?) do
          assert TreeMap.visible?(map, unquote(row), unquote(col))
        else
          refute TreeMap.visible?(map, unquote(row), unquote(col))
        end
      end
    end
  end
end

ExUnit.run()
```

<!-- livebook:{"output":true} -->

```
.........................
Finished in 0.00 seconds (0.00s async, 0.00s sync)
25 tests, 0 failures

Randomized with seed 359069
```

<!-- livebook:{"output":true} -->

```
%{excluded: 0, failures: 0, skipped: 0, total: 25}
```

With 16 trees visible on the edge and another 5 visible in the interior, a total of **`21`** trees are visible in this arrangement.

Consider your map; **how many trees are visible from outside the grid?**

```elixir
map = TreeMap.parse(tree_map)

Enum.flat_map(Enum.with_index(map), fn {map_row, row} ->
  Enum.filter(Enum.with_index(map_row), fn {_, col} ->
    TreeMap.visible?(map, row, col)
  end)
end)
|> Enum.count()
```

<!-- livebook:{"output":true} -->

```
1543
```

## Part Two

Content with the amount of tree cover available, the Elves just need to know the best spot to build their tree house: they would like to be able to see a lot of **trees**.

To measure the viewing distance from a given tree, look up, down, left, and right from that tree; stop if you reach an edge or at the first tree that is the same height or taller than the tree under consideration. (If a tree is right on the edge, at least one of its viewing distances will be zero.)

The Elves don't care about distant trees taller than those found by the rules above; the proposed tree house has large [eaves](https://en.wikipedia.org/wiki/Eaves) to keep it dry, so they wouldn't be able to see higher than the tree house anyway.

In the example above, consider the middle 5 in the second row:

```
3 0 3 7 3
2 5[5]1 2
6 5 3 3 2
3 3 5 4 9
3 5 3 9 0
```

* Looking up, its view is not blocked; it can see `1` tree (of height `3`).
* Looking left, its view is blocked immediately; it can see only `1` tree (of height `5`, right next to it).
* Looking right, its view is not blocked; it can see `2` trees.
* Looking down, its view is blocked eventually; it can see `2` trees (one of height `3`, then the tree of height `5` that blocks its view).

A tree's **scenic score** is found by **multiplying together** its viewing distance in each of the four directions. For this tree, this is **`4`** (found by multiplying `1 * 1 * 2 * 2`).

However, you can do even better: consider the tree of height `5` in the middle of the fourth row:

```
3 0 3 7 3
2 5 5 1 2
6 5 3 3 2
3 3[5]4 9
3 5 3 9 0
```

* Looking up, its view is blocked at `2` trees (by another tree with a height of `5`).
* Looking left, its view is not blocked; it can see `2` trees.
* Looking down, its view is also not blocked; it can see `1` tree.
* Looking right, its view is blocked at `2` trees (by a massive tree of height `9`).

This tree's scenic score is **`8`** (`2 * 2 * 1 * 2`); this is the ideal spot for the tree house.

```elixir
defmodule ScenicScoreTest do
  use ExUnit.Case

  setup do: [map: TreeMap.parse(unquote(sample_tree_map))]

  test "tree at (1, 2) has a scenic score of 4", %{map: map} do
    assert TreeMap.scenic_score(map, 1, 2) == 4
  end

  test "tree at (3, 2) has a scenic score of 8", %{map: map} do
    assert TreeMap.scenic_score(map, 3, 2) == 8
  end
end

ExUnit.run()
```

<!-- livebook:{"output":true} -->

```
..
Finished in 0.00 seconds (0.00s async, 0.00s sync)
2 tests, 0 failures

Randomized with seed 359069
```

<!-- livebook:{"output":true} -->

```
%{excluded: 0, failures: 0, skipped: 0, total: 2}
```

Consider each tree on your map. **What is the highest scenic score possible for any tree?**

```elixir
map = TreeMap.parse(tree_map)

Enum.flat_map(Enum.with_index(map), fn {map_row, row} ->
  Enum.map(Enum.with_index(map_row), fn {_, col} ->
    TreeMap.scenic_score(map, row, col)
  end)
end)
|> Enum.max()
```

<!-- livebook:{"output":true} -->

```
595080
```
