<!-- livebook:{"persist_outputs":true} -->

# Day 9: Rope Bridge

```elixir
Mix.install([
  {:kino, "~> 0.8.0"}
])

motions = Path.join(__DIR__, "day-09.data") |> File.read!()

:ok
```

<!-- livebook:{"output":true} -->

```
:ok
```

## Part One

This rope bridge creaks as you walk along it. You aren't sure how old it is, or whether it can even support your weight.

It seems to support the Elves just fine, though. The bridge spans a gorge which was carved out by the massive river far below you.

You step carefully; as you do, the ropes stretch and twist. You decide to distract yourself by modeling rope physics; maybe you can even figure out where not to step.

Consider a rope with a knot at each end; these knots mark the head and the tail of the rope. If the head moves far enough away from the tail, the tail is pulled toward the head.

Due to nebulous reasoning involving [Planck lengths](https://en.wikipedia.org/wiki/Planck_units#Planck_length), you should be able to model the positions of the knots on a two-dimensional grid. Then, by following a hypothetical **series of motions** (your puzzle input) for the head, you can determine how the tail will move.

Due to the aforementioned Planck lengths, the rope must be quite short; in fact, the head (`H`) and tail (`T`) must **always be touching** (diagonally adjacent and even overlapping both count as touching):

```
....
.TH.
....

....
.H..
..T.
....

...
.H. (H covers T)
...
```

If the head is ever two steps directly up, down, left, or right from the tail, the tail must also move one step in that direction so it remains close enough:

```
.....    .....    .....
.TH.. -> .T.H. -> ..TH.
.....    .....    .....

...    ...    ...
.T.    .T.    ...
.H. -> ... -> .T.
...    .H.    .H.
...    ...    ...
```

Otherwise, if the head and tail aren't touching and aren't in the same row or column, the tail always moves one step diagonally to keep up:

```
.....    .....    .....
.....    ..H..    ..H..
..H.. -> ..... -> ..T..
.T...    .T...    .....
.....    .....    .....

.....    .....    .....
.....    .....    .....
..H.. -> ...H. -> ..TH.
.T...    .T...    .....
.....    .....    .....
```

```elixir
defmodule Knot do
  defstruct x: 0, y: 0

  def step(%Knot{x: x, y: y}, :up), do: %Knot{x: x, y: y + 1}
  def step(%Knot{x: x, y: y}, :right), do: %Knot{x: x + 1, y: y}
  def step(%Knot{x: x, y: y}, :down), do: %Knot{x: x, y: y - 1}
  def step(%Knot{x: x, y: y}, :left), do: %Knot{x: x - 1, y: y}

  def chase(%Knot{x: x, y: y} = knot, %Knot{x: tx, y: ty} = target) do
    if touching?(knot, target) do
      knot
    else
      %Knot{x: chase_1d(x, tx), y: chase_1d(y, ty)}
    end
  end

  defp touching?(%Knot{x: x1, y: y1}, %Knot{x: x2, y: y2}) do
    abs(x1 - x2) <= 1 && abs(y1 - y2) <= 1
  end

  defp chase_1d(k, t) when k < t, do: k + 1
  defp chase_1d(k, t) when k == t, do: k
  defp chase_1d(k, t) when k > t, do: k - 1
end

defmodule Rope do
  def new(length), do: Enum.map(1..length, fn _ -> %Knot{} end)

  def steps(rope, {dir, dist}) do
    Enum.scan(1..dist, rope, fn _, rope ->
      head = hd(rope) |> Knot.step(dir)

      tail =
        tl(rope)
        |> Enum.scan(head, fn knot, target ->
          knot |> Knot.chase(target)
        end)

      [head | tail]
    end)
  end

  def draw(rope, x_range, y_range) do
    for y <- y_range do
      for x <- x_range do
        idx = Enum.find_index(rope, &(&1.x == x && &1.y == y))

        cond do
          idx == 0 -> "H"
          idx -> to_string(idx)
          x == 0 && y == 0 -> "s"
          true -> "."
        end
      end
    end
    |> Enum.reverse()
    |> Enum.join("\n")
  end
end
```

<!-- livebook:{"output":true} -->

```
{:module, Rope, <<70, 79, 82, 49, 0, 0, 15, ...>>, {:draw, 3}}
```

You just need to work out where the tail goes as the head follows a series of motions. Assume the head and the tail both start at the same position, overlapping.

For example:

```elixir
example_motions = """
R 4
U 4
L 3
D 1
R 4
D 1
L 5
R 2
"""
```

<!-- livebook:{"output":true} -->

```
"R 4\nU 4\nL 3\nD 1\nR 4\nD 1\nL 5\nR 2\n"
```

```elixir
defmodule Motions do
  def parse(input) do
    input |> String.trim() |> String.split("\n") |> Enum.map(&parse_line/1)
  end

  defp parse_line(line) do
    [dir, dist] = String.split(line, " ")
    {parse_dir(dir), parse_dist(dist)}
  end

  defp parse_dir("U"), do: :up
  defp parse_dir("R"), do: :right
  defp parse_dir("D"), do: :down
  defp parse_dir("L"), do: :left

  defp parse_dist(dist), do: String.to_integer(dist)
end
```

<!-- livebook:{"output":true} -->

```
{:module, Motions, <<70, 79, 82, 49, 0, 0, 9, ...>>, {:parse_dist, 1}}
```

This series of motions moves the head **right** four steps, then **up** four steps, then **left** three steps, then **down** one step, and so on. After each step, you'll need to update the position of the tail if the step means the head is no longer adjacent to the tail. Visually, these motions occur as follows (s marks the starting position as a reference point):

````elixir
example_motions
|> Motions.parse()
|> Enum.scan([Rope.new(2)], fn motion, steps ->
  steps |> List.last() |> Rope.steps(motion)
end)
|> Enum.flat_map(& &1)
|> Stream.zip(Stream.interval(500))
|> Kino.animate(fn {rope, _} ->
  rope
  |> Rope.draw(0..5, 0..4)
  |> String.replace("1", "T")
  |> then(&Kino.Markdown.new("```\n#{&1}\n```"))
end)
````

After simulating the rope, you can count up all of the positions the tail **visited at least once**. In this diagram, `s` again marks the starting position (which the tail also visited) and `#` marks other positions the tail visited:

```
..##..
...##.
.####.
....#.
s###..
```

So, there are **`13`** positions the tail visited at least once.

Simulate your complete hypothetical series of motions. **How many positions does the tail of the rope visit at least once?**

```elixir
motions
|> Motions.parse()
|> Enum.reduce({Rope.new(2), MapSet.new()}, fn motion, {rope, visited} ->
  steps = Rope.steps(rope, motion)

  {List.last(steps),
   Enum.reduce(steps, visited, fn rope, visited ->
     MapSet.put(visited, List.last(rope))
   end)}
end)
|> elem(1)
|> MapSet.size()
```

<!-- livebook:{"output":true} -->

```
6212
```

## Part Two

A rope snaps! Suddenly, the river is getting a lot closer than you remember. The bridge is still there, but some of the ropes that broke are now whipping toward you as you fall through the air!

The ropes are moving too quickly to grab; you only have a few seconds to choose how to arch your body to avoid being hit. Fortunately, your simulation can be extended to support longer ropes.

Rather than two knots, you now must simulate a rope consisting of ten knots. One knot is still the head of the rope and moves according to the series of motions. Each knot further down the rope follows the knot in front of it using the same rules as before.

Using the same series of motions as the above example, but with the knots marked `H`, `1`, `2`, ..., `9`, the motions now occur as follows:

````elixir
example_motions
|> Motions.parse()
|> Enum.scan([Rope.new(10)], fn motion, steps ->
  steps |> List.last() |> Rope.steps(motion)
end)
|> Enum.flat_map(& &1)
|> Stream.zip(Stream.interval(500))
|> Kino.animate(fn {rope, _} ->
  rope
  |> Rope.draw(0..5, 0..4)
  |> then(&Kino.Markdown.new("```\n#{&1}\n```"))
end)
````

Now, you need to keep track of the positions the new tail, 9, visits. In this example, the tail never moves, and so it only visits 1 position. However, be careful: more types of motion are possible than before, so you might want to visually compare your simulated rope to the one above.

Here's a larger example. These motions occur as follows (individual steps are not shown):

````elixir
"""
R 5
U 8
L 8
D 3
R 17
D 10
L 25
U 20
"""
|> Motions.parse()
|> Enum.scan([Rope.new(10)], fn motion, steps ->
  steps |> List.last() |> Rope.steps(motion)
end)
|> Enum.map(&List.last/1)
|> Stream.zip(Stream.interval(1_000))
|> Kino.animate(fn {rope, _} ->
  rope
  |> Rope.draw(-11..14, -5..15)
  |> then(&Kino.Markdown.new("```\n#{&1}\n```"))
end)
````

Now, the tail (`9`) visits **`36`** positions (including `s`) at least once:

```
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
#.........................
#.............###.........
#............#...#........
.#..........#.....#.......
..#..........#.....#......
...#........#.......#.....
....#......s.........#....
.....#..............#.....
......#............#......
.......#..........#.......
........#........#........
.........########.........
```

Simulate your complete series of motions on a larger rope with ten knots. **How many positions does the tail of the rope visit at least once?**

```elixir
motions
|> Motions.parse()
|> Enum.reduce({Rope.new(10), MapSet.new()}, fn motion, {rope, visited} ->
  steps = Rope.steps(rope, motion)

  {List.last(steps),
   Enum.reduce(steps, visited, fn rope, visited ->
     MapSet.put(visited, List.last(rope))
   end)}
end)
|> elem(1)
|> MapSet.size()
```

<!-- livebook:{"output":true} -->

```
2522
```
