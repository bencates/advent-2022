<!-- livebook:{"persist_outputs":true} -->

# Day 5: Supply Stacks

```elixir
Mix.install(nimble_parsec: "~> 1.2")

input = Path.join(__DIR__, "day-05.data") |> File.read!()

:ok
```

<!-- livebook:{"output":true} -->

```
:ok
```

## Part One

The expedition can depart as soon as the final supplies have been unloaded from the ships. Supplies are stored in stacks of marked **crates**, but because the needed supplies are buried under many other crates, the crates need to be rearranged.

The ship has a giant cargo crane capable of moving crates between stacks. To ensure none of the crates get crushed or fall over, the crane operator will rearrange them in a series of carefully-planned steps. After the crates are rearranged, the desired crates will be at the top of each stack.

The Elves don't want to interrupt the crane operator during this delicate procedure, but they forgot to ask her which crate will end up where, and they want to be ready to unload them as soon as possible so they can embark.

They do, however, have a drawing of the starting stacks of crates and the rearrangement procedure (your puzzle input). For example:

```elixir
sample_input = """
    [D]    
[N] [C]    
[Z] [M] [P]
 1   2   3 

move 1 from 2 to 1
move 3 from 1 to 3
move 2 from 2 to 1
move 1 from 1 to 2
"""
```

<!-- livebook:{"output":true} -->

```
"    [D]    \n[N] [C]    \n[Z] [M] [P]\n 1   2   3 \n\nmove 1 from 2 to 1\nmove 3 from 1 to 3\nmove 2 from 2 to 1\nmove 1 from 1 to 2\n"
```

In this example, there are three stacks of crates. Stack 1 contains two crates: crate Z is on the bottom, and crate N is on top. Stack 2 contains three crates; from bottom to top, they are crates `M`, `C`, and `D`. Finally, stack 3 contains a single crate, `P`.

```elixir
defmodule CargoStacks do
  import NimbleParsec

  crate = ignore(string("[")) |> utf8_char([?A..?Z]) |> ignore(string("]"))
  empty_space = string("   ") |> replace(nil)

  row =
    repeat(choice([crate, empty_space]) |> ignore(optional(string(" "))))
    |> ignore(string("\n"))
    |> wrap()

  stack_labels = repeat(utf8_char([?0..?9 | ' ']))

  stacks =
    repeat(row)
    |> ignore(stack_labels |> string("\n\n"))
    |> reduce(:build_stacks)
    |> unwrap_and_tag(:stacks)

  defp build_stacks(rows) do
    Enum.map(Enum.zip(rows), fn stack ->
      stack |> Tuple.to_list() |> Enum.reject(&is_nil/1)
    end)
  end

  command =
    ignore(string("move "))
    |> integer(min: 1)
    |> ignore(string(" from "))
    |> integer(min: 1)
    |> ignore(string(" to "))
    |> integer(min: 1)
    |> ignore(string("\n"))
    |> wrap()

  defparsec(:parse, concat(stacks, repeat(command) |> tag(:commands)))

  def parse!(input) do
    {:ok, [stacks: stacks, commands: commands], "", _, _, _} = parse(input)
    {stacks, commands}
  end

  def move(stacks, count, from, to) do
    from = from - 1
    to = to - 1

    {moving, remainder} = Enum.split(Enum.at(stacks, from), count)

    stacks
    |> List.replace_at(from, remainder)
    |> List.replace_at(to, Enum.reverse(moving) ++ Enum.at(stacks, to))
  end

  def move_group(stacks, count, from, to) do
    from = from - 1
    to = to - 1

    {moving, remainder} = Enum.split(Enum.at(stacks, from), count)

    stacks
    |> List.replace_at(from, remainder)
    |> List.replace_at(to, moving ++ Enum.at(stacks, to))
  end
end

CargoStacks.parse!(sample_input) |> elem(0)
```

<!-- livebook:{"output":true} -->

```
['NZ', 'DCM', 'P']
```

Then, the rearrangement procedure is given. In each step of the procedure, a quantity of crates is moved from one stack to a different stack. In the first step of the above rearrangement procedure, one crate is moved from stack 2 to stack 1, resulting in this configuration:

```
[D]        
[N] [C]    
[Z] [M] [P]
 1   2   3 
```

In the second step, three crates are moved from stack 1 to stack 3. Crates are moved **one at a time**, so the first crate to be moved (`D`) ends up below the second and third crates:

```
        [Z]
        [N]
    [C] [D]
    [M] [P]
 1   2   3 
```

Then, both crates are moved from stack 2 to stack 1. Again, because crates are moved **one at a time**, crate `C` ends up below crate `M`:

```
        [Z]
        [N]
[M]     [D]
[C]     [P]
 1   2   3 
```

Finally, one crate is moved from stack 1 to stack 2:

```
        [Z]
        [N]
        [D]
[C] [M] [P]
 1   2   3 
```

```elixir
{stacks, commands} = CargoStacks.parse!(sample_input)

Enum.reduce(commands, stacks, fn [count, from, to], stacks ->
  CargoStacks.move(stacks, count, from, to) |> dbg()
end)
```

<!-- livebook:{"output":true} -->

```
[#cell:lljx2hxnxx5shomuovc6khbjpfej5xn5:4: (file)]
CargoStacks.move(stacks, count, from, to) #=> ['DNZ', 'CM', 'P']

[#cell:lljx2hxnxx5shomuovc6khbjpfej5xn5:4: (file)]
CargoStacks.move(stacks, count, from, to) #=> [[], 'CM', 'ZNDP']

[#cell:lljx2hxnxx5shomuovc6khbjpfej5xn5:4: (file)]
CargoStacks.move(stacks, count, from, to) #=> ['MC', [], 'ZNDP']

[#cell:lljx2hxnxx5shomuovc6khbjpfej5xn5:4: (file)]
CargoStacks.move(stacks, count, from, to) #=> ['C', 'M', 'ZNDP']


```

<!-- livebook:{"output":true} -->

```
['C', 'M', 'ZNDP']
```

The Elves just need to know **which crate will end up on top of each stack**; in this example, the top crates are `C` in stack 1, `M` in stack 2, and `Z` in stack 3, so you should combine these together and give the Elves the message **`CMZ`**.

**After the rearrangement procedure completes, what crate ends up on top of each stack?**

```elixir
{stacks, commands} = CargoStacks.parse!(input)

stacks =
  Enum.reduce(commands, stacks, fn [count, from, to], stacks ->
    CargoStacks.move(stacks, count, from, to)
  end)

Enum.map(stacks, &hd/1)
```

<!-- livebook:{"output":true} -->

```
'BZLVHBWQF'
```

## Part Two

As you watch the crane operator expertly rearrange the crates, you notice the process isn't following your prediction.

Some mud was covering the writing on the side of the crane, and you quickly wipe it away. The crane isn't a CrateMover 9000 - it's a **CrateMover 9001**.

The CrateMover 9001 is notable for many new and exciting features: air conditioning, leather seats, an extra cup holder, and **the ability to pick up and move multiple crates at once**.

Again considering the example above, the crates begin in the same configuration:

```
    [D]    
[N] [C]    
[Z] [M] [P]
 1   2   3 
```

Moving a single crate from stack 2 to stack 1 behaves the same as before:

```
[D]        
[N] [C]    
[Z] [M] [P]
 1   2   3 
```

However, the action of moving three crates from stack 1 to stack 3 means that those three moved crates **stay in the same order**, resulting in this new configuration:

```
        [D]
        [N]
    [C] [Z]
    [M] [P]
 1   2   3 
```

Next, as both crates are moved from stack 2 to stack 1, they **retain their order** as well:

```
        [D]
        [N]
[C]     [Z]
[M]     [P]
 1   2   3 
```

Finally, a single crate is still moved from stack 1 to stack 2, but now it's crate `C` that gets moved:

```
        [D]
        [N]
        [Z]
[M] [C] [P]
 1   2   3 
```

In this example, the CrateMover 9001 has put the crates in a totally different order: **`MCD`.**

```elixir
{stacks, commands} = CargoStacks.parse!(sample_input)

Enum.reduce(commands, stacks, fn [count, from, to], stacks ->
  CargoStacks.move_group(stacks, count, from, to) |> dbg()
end)
```

<!-- livebook:{"output":true} -->

```
[#cell:jkdxtdneu2mzk4wsbju2g4d4ptxofr7f:4: (file)]
CargoStacks.move_group(stacks, count, from, to) #=> ['DNZ', 'CM', 'P']

[#cell:jkdxtdneu2mzk4wsbju2g4d4ptxofr7f:4: (file)]
CargoStacks.move_group(stacks, count, from, to) #=> [[], 'CM', 'DNZP']

[#cell:jkdxtdneu2mzk4wsbju2g4d4ptxofr7f:4: (file)]
CargoStacks.move_group(stacks, count, from, to) #=> ['CM', [], 'DNZP']

[#cell:jkdxtdneu2mzk4wsbju2g4d4ptxofr7f:4: (file)]
CargoStacks.move_group(stacks, count, from, to) #=> ['M', 'C', 'DNZP']


```

<!-- livebook:{"output":true} -->

```
['M', 'C', 'DNZP']
```

Before the rearrangement process finishes, update your simulation so that the Elves know where they should stand to be ready to unload the final supplies. **After the rearrangement procedure completes, what crate ends up on top of each stack?**

```elixir
{stacks, commands} = CargoStacks.parse!(input)

stacks =
  Enum.reduce(commands, stacks, fn [count, from, to], stacks ->
    CargoStacks.move_group(stacks, count, from, to)
  end)

Enum.map(stacks, &hd/1)
```

<!-- livebook:{"output":true} -->

```
'TDGJQTZSL'
```
