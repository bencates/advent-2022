<!-- livebook:{"persist_outputs":true} -->

# Day 2: Rock Paper Scissors

```elixir
Mix.install(nimble_parsec: "~> 1.2")

import ExUnit.Assertions, only: [assert: 1]

input = Path.join(__DIR__, "day-02.data") |> File.read!()
```

<!-- livebook:{"output":true} -->

```
"A Y\nA Y\nB X\nA Y\nC Y\nB Y\nB Z\nB Y\nA X\nB X\nC Z\nB Y\nB Y\nB Y\nB Y\nC X\nA Z\nB X\nA Y\nA Y\nB Y\nA X\nC X\nB Y\nA Y\nB Y\nC X\nA Z\nB Y\nA X\nA Y\nB Y\nA Y\nA X\nB Y\nB Y\nB Y\nB X\nA X\nC Y\nA X\nC Z\nB Z\nA Z\nA Z\nC X\nA X\nA X\nB Y\nB Y\nC Y\nB Y\nB X\nB Y\nB Y\nB X\nC X\nA X\nA X\nA Z\nC X\nA Z\nA X\nB Y\nB Y\nA Y\nA Z\nB Y\nB X\nB Y\nB Y\nB X\nC Z\nA X\nB X\nB X\nA Y\nB X\nC Z\nB Y\nA X\nC Z\nA Y\nB Y\nB Y\nA X\nA Z\nB Y\nB Y\nA Z\nA Z\nB Y\nA X\nA Z\nC Y\nA X\nB Y\nC X\nA X\nB Y\nA X\nA Z\nC X\nA Y\nA X\nB X\nA X\nB Z\nB X\nB Y\nA X\nA Y\nB Y\nB Z\nC X\nB X\nA X\nC Z\nC Y\nA Y\nB Y\nA X\nB Y\nC Y\nB Y\nC Y\nB Y\nC Z\nC X\nA Y\nB Y\nB X\nA X\nA X\nA Y\nA Y\nB Y\nA Z\nB Y\nA X\nC X\nB Y\nB X\nC X\nA Y\nB Z\nB Y\nB X\nB Y\nB Y\nC Z\nB X\nB Z\nA X\nB Y\nA X\nB Y\nC Z\nA X\nA Y\nB Z\nA X\nB X\nA Z\nB Y\nA Y\nA X\nB Y\nC Z\nB X\nB Y\nC X\nB X\nA X\nC X\nB X\nB Y\nB Z\nB Y\nB Y\nA X\nB Y\nA X\nA X\nB X\nA X\nA X\nB Y\nB Y\nB Y\nC X\nB Y\nA Z\nA X\nB Y\nA Y\nA Z\nB Y\nA Z\nC X\nB Y\nC X\nB Y\nB Y\nA X\nB Y\nA X\nB Y\nB X\nB Y\nB Y\nB Z\nB Y\nA Y\nA X\nA X\nA X\nC Z\nA Y\nA X\nC Y\nA X\nB Z\nB Y\nB Y\nB Y\nA Z\nB Y\nB X\nA Z\nB Y\nA Y\nB Y\nA Z\nB Y\nA X\nB Z\nA Z\nC X\nB Y\nB Y\nB Y\nB X\nA X\nB X\nA Y\nB X\nB Y\nB Y\nA X\nB Y\nB Z\nB Y\nB Y\nB X\nB X\nC Z\nA Y\nB Y\nB Y\nB Y\nB X\nA X\nB X\nA Y\nA Z\nA Z\nA Z\nA X\nA Y\nB Y\nC Y\nA Z\nA Z\nA Z\nC X\nA X\nC X\nA Z\nB X\nA Y\nB Y\nA Z\nB Y\nB X\nC Y\nA Z\nA X\nC X\nC X\nC X\nB Y\nA X\nA Y\nC X\nB Y\nC Z\nB Y\nB Y\nA Y\nB Y\nA Y\nB Y\nB Y\nB X\nA X\nA X\nA Z\nA Y\nB X\nB Y\nA Z\nC X\nA Y\nA X\nB Y\nB Y\nB X\nA Z\nB Y\nA X\nA X\nB Y\nA X\nC X\nA Z\nA Z\nB Y\nA Z\nA Y\nA Y\nA Y\nB Y\nA Y\nA Z\nA Y\nA X\nA Z\nB Y\nB Y\nC X\nB X\nC Z\nB X\nA Z\nC Y\nB Y\nA X\nB Y\nA Y\nA X\nB Y\nB X\nC Z\nA Z\nA Z\nB Y\nA Z\nB Y\nB Y\nA X\nA X\nA X\nB Z\nA Y\nB Z\nA Y\nC Y\nA X\nB X\nA Y\nB X\nB Y\nA X\nA Y\nA X\nC X\nC Z\nB Y\nA Y\nB Y\nB Z\nA X\nB Y\nC Y\nA Y\nA Z\nB Y\nA X\nA Z\nA Y\nB Y\nB Y\nA Z\nC Z\nB X\nA Y\nC Y\nA X\nA X\nA X\nB Y\nC Z\nA X\nA X\nB X\nA X\nA X\nA Y\nA X\nC X\nA X\nA Y\nB Z\nA X\nC X\nA X\nB X\nA Y\nA X\nA X\nA X\nC Z\nA X\nA X\nB X\nB Y\nA Y\nA X\nA Z\nA Y\nA Y\nA Y\nA Z\nB X\nA X\nA X\nA X\nA Z\nB Y\nA X\nA Y\nB X\nC X\nB Y\nA Z\nA Z\nA Z\nB Z\nB Y\nA Z\nA Z\nA X\nA Y\nA X\nA X\nA X\nA Y\nA Z\nB Y\nA Z\nB X\nB X\nA Y\nA X\nB X\nB X\nC Z\nB X\nA X\nA Z\nC X\nA X\nA X\nB Z\nA X\nB X\nA X\nA X\nA X\nB Z\nA Z\nA Y\nA Z\nB Y\nA X\nA Y\nA Y\nB X\nC Z\nA X\nA X\nA X\nB Y\nB Y\nA X\nA X\nB Y\nA X\nB X\nA Y\nC X\nB X\nB Y\nC Z\nB Y\nA X\nC Z\nB Z\nB Y\nA Z\nB X\nB Y\nB X\nB Y\nB Y\nB Y\nB X\nB Y\nA X\nB Z\nB Y\nC X\nB Y\nC X\nA Z\nB X\nA Z\nA X\nA Z\nA Y\nA X\nA Z\nB Y\nB X\nB X\nB Y\nB X\nB X\nB Z\nB Z\nB Y\nA X\nC X\nB Y\nB X\nB X\nC X\nB X\nB X\nB Y\nA Z\nA Z\nB Y\nC Y\nA Z\nB Y\nB Z\nA Z\nC Y\nC X\nA X\nB Y\nA Y\nB X\nB Y\nB Z\nA Y\nA Z\nA X\nA X\nC X\nB Z\nA Y\nA Z\nB Y\nA Y\nA Z\nB Y\nA Z\nC Z\nB Y\nA X\nA Z\nB Y\nB Y\nA Y\nA X\nC Z\nB Y\nB X\nA X\nC Z\nB Y\nB Y\nB Z\nB Y\nA Y\nB Y\nA Z\nC Z\nB Y\nB X\nB Z\nB X\nA X\nA Y\nA Z\nA Z\nA X\nA X\nB Y\nB X\nA Z\nA X\nB Y\nB Y\nB X\nA Y\nB Y\nC Y\nB Y\nA X\nB Y\nB X\nB Z\nA X\nB Y\nB Y\nA X\nA X\nA X\nC X\nB X\nA Z\nB X\nC X\nB Y\nA Y\nB Y\nC X\nB Y\nB Y\nB X\nA X\nA X\nA X\nA X\nB Y\nB Y\nB Z\nA Z\nA Z\nA Z\nA Y\nB X\nA Y\nB Y\nC Z\nC X\nB Y\nB Y\nB Y\nB Y\nA X\nA X\nA Z\nC Y\nB X\nA Y\nB Y\nB Y\nC Z\nB X\nA X\nA X\nA Z\nC X\nB Y\nB X\nC Z\nB X\nA Y\nB X\nB X\nB Z\nC X\nA Z\nB Y\nB X\nB Y\nB Y\nA X\nB Y\nA X\nA X\nA Y\nA Z\nB Z\nB Y\nA Y\nA Y\nC Z\nA Y\nA X\nB Y\nB Y\nC Z\nB Y\nA X\nB X\nA X\nB Y\nA X\nA Z\nB Z\nB Y\nA X\nC X\nA X\nA X\nB Y\nA Y\nA X\nA Z\nB Y\nC Z\nC X\nA X\nB X\nB X\nB Y\nB X\nB Y\nB Y\nA Z\nA Y\nA Z\nA X\nB Y\nB X\nB Y\nC X\nB Z\nB X\nC X\nB Y\nA X\nA Y\nA Y\nB X\nA X\nB Y\nB X\nA X\nB X\nB Y\nC Y\nA Z\nC Z\nB X\nB X\nB Y\nA Z\nC Y\nB X\nA Z\nB Y\nA Y\nA Z\nB Y\nA X\nA Y\nC Z\nB Y\nA X\nB Y\nB X\nA Y\nB Y\nC X\nA Y\nB Y\nB Y\nA X\nC Z\nB Y\nB X\nA X\nB X\nB Y\nA X\nB Y\nB Y\nB Z\nA X\nC Z\nA Y\nB X\nA X\nC X\nB Z\nB Y\nA Z\nB X\nA Z\nB Y\nC Z\nA X\nB Y\nB X\nB Y\nA X\nB Y\nA X\nA Y\nA Y\nA X\nB Y\nA X\nB Y\nA X\nA Y\nA Y\nC X\nB X\nB Y\nA Z\nB X\nA X\nB Y\nA Y\nB X\nB Y\nA Z\nB X\nB X\nA Y\nB Y\nA Z\nB X\nB Y\nA Y\nA X\nA X\nB X\nC Z\nA Y\nB Y\nA Z\nA Z\nA Z\nB Y\nA Y\nA X\nA Z\nB X\nB Y\nC Y\nA X\nA X\nC X\nA Z\nB X\nB Y\nB X\nB Y\nB Y\nB Y\nA X\nB Z\nB Z\nB Z\nB Y\nA X\nA X\nB Y\nA Z\nB Y\nC Y\nB X\nB X\nB X\nB Y\nB Y\nA Y\nA X\nB X\nA X\nA Z\nA Y\nC Y\nB Y\nB Z\nB Z\nA Y\nB Y\nA Z\nB X\nB Y\nB Y\nB X\nB X\nB Y\nA X\nC Z\nB Y\nA X\nA X\nA Y\nB X\nB Y\nA Z\nA Y\nB Y\nA Y\nA X\nA Z\nA Y\nA X\nA X\nA Y\nB X\nA X\nB Y\nB Y\nB X\nB Y\nB Y\nB Y\nA Y\nB X\nA X\nC Y\nA Y\nB Y\nC X\nC X\nB Y\nB Z\nA Z\nA Y\nB Y\nB Y\nA Y\nB Z\nA Z\nA X\nB Y\nA X\nC Z\nB Y\nB Y\nA Z\nA Y\nB Y\nA Z\nB Y\nA Z\nA Y\nA Z\nC Y\nA Z\nA X\nA X\nA Z\nA X\nB Y\nA Z\nB X\nA Y\nB Y\nA Z\nB X\nB Z\nB X\nA Y\nA Y\nB Y\nA Z\nA X\nB X\nA Z\nA X\nB Y\nB X\nC X\nA Z\nC X\nC X\nB Z\nA X\nB Y\nA Y\nA Y\nA Z\nB Y\nC X\nA Z\nA X\nA Y\nA Z\nA X\nA Y\nA Y\nA X\nC X\nB Y\nB Y\nB Y\nA Z\nB Y\nA Z\nB Y\nC X\nB Y\nA X\nB X\nB Y\nC X\nA Z\nB Y\nB Y\nB Y\n" <> ...
```

## Part One

The Elves begin to set up camp on the beach. To decide whose tent gets to be closest to the snack storage, a giant [Rock Paper Scissors](https://en.wikipedia.org/wiki/Rock_paper_scissors) tournament is already in progress.

Rock Paper Scissors is a game between two players. Each game contains many rounds; in each round, the players each simultaneously choose one of Rock, Paper, or Scissors using a hand shape. Then, a winner for that round is selected: Rock defeats Scissors, Scissors defeats Paper, and Paper defeats Rock. If both players choose the same shape, the round instead ends in a draw.

```elixir
defmodule Round do
  def throw(:rock, :scissors), do: :win
  def throw(:scissors, :paper), do: :win
  def throw(:paper, :rock), do: :win
  def throw(shape, shape), do: :draw
  def throw(_, _), do: :lose
end
```

<!-- livebook:{"output":true} -->

```
{:module, Round, <<70, 79, 82, 49, 0, 0, 6, ...>>, {:outcome, 2}}
```

Appreciative of your help yesterday, one Elf gives you an **encrypted strategy guide** (your puzzle input) that they say will be sure to help you win. "The first column is what your opponent is going to play: `A` for Rock, `B` for Paper, and `C` for Scissors. The second column--" Suddenly, the Elf is called away to help with someone's tent.

The second column, you reason, must be what you should play in response: `X` for Rock, `Y` for Paper, and `Z` for Scissors. Winning every time would be suspicious, so the responses must have been carefully chosen.

```elixir
defmodule StrategyGuide do
  import NimbleParsec

  rock = choice([string("A"), string("X")]) |> tag(:rock)
  paper = choice([string("B"), string("Y")]) |> tag(:paper)
  scissors = choice([string("C"), string("Z")]) |> tag(:scissors)

  defp map_round([{opponent_shape, _}, {player_shape, _}]), do: [player_shape, opponent_shape]

  round =
    choice([rock, paper, scissors])
    |> ignore(string(" "))
    |> choice([rock, paper, scissors])
    |> ignore(string("\n"))
    |> wrap()
    |> map(:map_round)

  defparsecp(:parse_guide, repeat(round))

  def decode(input) do
    {:ok, data, "", _, _, _} = parse_guide(input)
    data
  end
end
```

<!-- livebook:{"output":true} -->

```
{:module, StrategyGuide, <<70, 79, 82, 49, 0, 0, 34, ...>>, {:decode, 1}}
```

The winner of the whole tournament is the player with the highest score. Your **total score** is the sum of your scores for each round. The score for a single round is the score for the **shape you selected** (1 for Rock, 2 for Paper, and 3 for Scissors) plus the score for the **outcome of the round** (0 if you lost, 3 if the round was a draw, and 6 if you won).

```elixir
defmodule Tournament do
  def score_round(round) do
    shape_score =
      case hd(round) do
        :rock -> 1
        :paper -> 2
        :scissors -> 3
      end

    outcome_score =
      case apply(Round, :throw, round) do
        :win -> 6
        :draw -> 3
        :lose -> 0
      end

    shape_score + outcome_score
  end

  def score(rounds) do
    Enum.reduce(rounds, 0, &(score_round(&1) + &2))
  end
end
```

<!-- livebook:{"output":true} -->

```
{:module, Tournament, <<70, 79, 82, 49, 0, 0, 9, ...>>, {:score, 1}}
```

Since you can't be sure if the Elf is trying to help you or trick you, you should calculate the score you would get if you were to follow the strategy guide.

For example, suppose you were given the following strategy guide:

```elixir
sample_input = """
A Y
B X
C Z
"""
```

<!-- livebook:{"output":true} -->

```
"A Y\nB X\nC Z\n"
```

This strategy guide predicts and recommends the following:

* In the first round, your opponent will choose Rock (`A`), and you should choose Paper (`Y`). This ends in a win for you with a score of **8** (2 because you chose Paper + 6 because you won).
* In the second round, your opponent will choose Paper (`B`), and you should choose Rock (`X`). This ends in a loss for you with a score of **1** (1 + 0).
* The third round is a draw with both players choosing Scissors, giving you a score of 3 + 3 = **6**.

In this example, if you were to follow the strategy guide, you would get a total score of **`15`** (8 + 1 + 6).

```elixir
strategy_guide = StrategyGuide.decode(sample_input)
[round_1, round_2, round_3] = strategy_guide

assert apply(Round, :throw, round_1) == :win
assert Tournament.score_round(round_1) == 8

assert apply(Round, :throw, round_2) == :lose
assert Tournament.score_round(round_2) == 1

assert apply(Round, :throw, round_3) == :draw
assert Tournament.score_round(round_3) == 6

assert Tournament.score(strategy_guide) == 15
```

<!-- livebook:{"output":true} -->

```
true
```

**What would your total score be if everything goes exactly according to your strategy guide?**

```elixir
input |> StrategyGuide.decode() |> Tournament.score()
```

<!-- livebook:{"output":true} -->

```
11906
```

## Part Two

The Elf finishes helping with the tent and sneaks back over to you. "Anyway, the second column says how the round needs to end: `X` means you need to lose, `Y` means you need to end the round in a draw, and `Z` means you need to win. Good luck!"

```elixir
defmodule CorrectedStrategyGuide do
  import NimbleParsec

  rock = string("A") |> tag(:rock)
  paper = string("B") |> tag(:paper)
  scissors = string("C") |> tag(:scissors)

  lose = string("X") |> tag(:lose)
  draw = string("Y") |> tag(:draw)
  win = string("Z") |> tag(:win)

  defp map_round([{opponent_shape, _}, {result, _}]) do
    player_shape =
      Enum.find(
        [:rock, :paper, :scissors],
        fn shape -> Round.throw(shape, opponent_shape) == result end
      )

    [player_shape, opponent_shape]
  end

  round =
    choice([rock, paper, scissors])
    |> ignore(string(" "))
    |> choice([win, draw, lose])
    |> ignore(string("\n"))
    |> wrap()
    |> map(:map_round)

  defparsecp(:parse_guide, repeat(round))

  def decode(input) do
    {:ok, data, "", _, _, _} = parse_guide(input)
    data
  end
end
```

<!-- livebook:{"output":true} -->

```
{:module, CorrectedStrategyGuide, <<70, 79, 82, 49, 0, 0, 22, ...>>, {:decode, 1}}
```

The total score is still calculated in the same way, but now you need to figure out what shape to choose so the round ends as indicated. The example above now goes like this:

* In the first round, your opponent will choose Rock (`A`), and you need the round to end in a draw (`Y`), so you also choose Rock. This gives you a score of 1 + 3 = **4**.
* In the second round, your opponent will choose Paper (`B`), and you choose Rock so you lose (`X`) with a score of 1 + 0 = **1**.
* In the third round, you will defeat your opponent's Scissors with Rock for a score of 1 + 6 = **7**.

Now that you're correctly decrypting the ultra top secret strategy guide, you would get a total score of **`12`**.

```elixir
strategy_guide = CorrectedStrategyGuide.decode(sample_input)
[round_1, round_2, round_3] = strategy_guide

assert apply(Round, :throw, round_1) == :draw
assert Tournament.score_round(round_1) == 4

assert apply(Round, :throw, round_2) == :lose
assert Tournament.score_round(round_2) == 1

assert apply(Round, :throw, round_3) == :win
assert Tournament.score_round(round_3) == 7

assert Tournament.score(strategy_guide) == 12
```

<!-- livebook:{"output":true} -->

```
true
```

Following the Elf's instructions for the second column, **what would your total score be if everything goes exactly according to your strategy guide?**

```elixir
input |> CorrectedStrategyGuide.decode() |> Tournament.score()
```

<!-- livebook:{"output":true} -->

```
11186
```
